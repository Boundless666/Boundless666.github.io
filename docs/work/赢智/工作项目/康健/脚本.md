# MES

#### GenCode_BOMWORKSUB1

> 生成派工单编号脚本

```sql

/****** Object:  UserDefinedFunction [dbo].[GenCode_BOMWORKSUB1]    Script Date: 2024/3/24 0:31:30 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


ALTER function [dbo].[GenCode_BOMWORKSUB1](
   @FDate datetime 
)
returns varchar(30)
as
begin
  declare  @FRule varchar(30) set @FRule = 'TYYMMDDSSSS'   --单号结构先写死
  declare  @FType varchar(4) SET @FType = 'T'             --单号第一个字母
  declare  @FRealLen int  set @FRealLen = 14              --最大长度
  declare  @FSerPos int set @FSerPos =-1
  declare  @FSerLen int set @FSerLen =0
  declare  @FNr varchar(14) set @FNr =''
  declare  @yy int, @mm int, @dd int, @i int, @iLn int, @c varchar(1),@isHasFixSymbol bit

  if len(@FType) = 0
    set @FType = '-'
  set @yy = year(@FDate)
  set @mm = MONTH(@FDate)
  set @dd = DAY(@FDate)
 if len(@FRule) < @FRealLen
    set @iLn  = len(@FRule)
  else
    set @iLn  = @FRealLen

  set @i  = 1
  set @isHasFixSymbol  = 0
  while (@i <=  @iLn)
  begin
    set @c = substring(@FRule, @i   ,1)

    if @c = 'Y'
    begin
      if substring(@FRule, @i+1  ,1) = 'Y'
      begin
        if (substring(@FRule, @i+2  ,1) = 'Y') and (substring(@FRule, @i+3  ,1) = 'Y')
        begin
          set @FNr = @FNr +   RIGHT('0000'+CAST(@yy as varchar(4)),4)
          set @i = @i + 3
        end
        else  begin
          set @FNr = @FNr +   RIGHT('0'+CAST(@yy as varchar(4)),2)
          set @i = @i + 1
        end
      end
      else
        set @FNr = @FNr +   RIGHT(CAST(@yy as varchar(4)),1)
    end
    else if @c = 'M'
    begin
      set @FNr = @FNr +   RIGHT('00'+CAST(@mm as varchar(2)),2)
      set @i = @i + 1
    end
    else if @c = 'D'
    begin
      set @FNr = @FNr +   RIGHT('00'+CAST(@dd as varchar(2)),2)
      set @i = @i + 1
    end
    else if @c = 'T'
      set @FNr = @FNr + @FType

    else if @c = 'S'
    begin
      if @FSerPos = -1
      begin
        if @isHasFixSymbol =1
          set @FSerPos = @i - 2
        else
          set @FSerPos = @i
      end
      set @FSerLen = @FSerLen + 1
    end

    else if @c = '['
    begin
      set @i = @i + 1
      while @i <= @iLn
      begin
        set @c  = substring(@FRule, @i  ,1)
        if @c = ']'
        begin
          set @isHasFixSymbol = 1
          break
        end
        set @FNr = @FNr + @c
        set @i = @i + 1
      end
      set @i = @i - 1
    end
    set @i = @i + 1
  end

  if @FSerLen > 0
  begin
    declare @iSer int set @iSer = 0
    declare @param1 varchar(500) set @param1 = @FNr + '%'
    declare @s varchar(100)  set @s = (SELECT  MAX(CODE) FROM BOMWORKSUB1 WHERE CODE LIKE @param1)
    if len(@s) > 0
    begin
       declare @tSer varchar(100) set @tSer = substring(@s, @FSerPos, @FSerLen)
       if ISNUMERIC(@tSer) = 1
         set @iSer = CAST(@tSer as int)
    end
    declare @t varchar(20)
    declare @isUnique bit set @isUnique = 0
    while  @isUnique = 0
    begin
      set @iSer = @iSer + 1
      set @t =  REPLICATE('0', @FSerLen - len(cast(@iSer as varchar))) + cast(@iSer as varchar)
      declare @param2 varchar(500) set @param2 = @FNr + @t
      declare @ii int set @ii = (SELECT count(*) FROM BOMWORKSUB1 WHERE CODE =@param2)
      if (@ii = 0)
          set @isUnique = 1
      if @isUnique > 0
          set @FNr = @FNr + @t
    end
  end
  return @FNr
end


```





# ERP

#### GenBatchCode 生成批次号

> 此处用于到货检验  保存时生成 QMSTA TAC04 验收批次

```sql
/****** Object:  UserDefinedFunction [dbo].[GenBatchCode]    Script Date: 2024-04-10 10:43:43 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER FUNCTION [dbo].[GenBatchCode] 
(
	@TableName NVARCHAR(20),
    @orderType CHAR(4), --单别
	@orderCode CHAR(20), -- 单号
	@orderSerial CHAR(4) -- 单序号
)
RETURNS varchar(4)
AS
BEGIN
  declare @result varchar(4) 
  declare @maxcode varchar(4)
  
  SET @maxcode = CASE
					WHEN @TableName = 'QMSTA' THEN (SELECT MAX(TAC04) FROM dbo.QMSTA WHERE TA001=@orderType AND TA002 =@orderCode AND TA003 = @orderSerial)	-- 最大批次号
					ELSE NULL
				 END
  if @maxcode is null
    set @result = RIGHT('0000000001', 4)
  ELSE
  BEGIN
	DECLARE @tmp VARCHAR(4)
	SET @tmp = ISNULL(MAX(CAST(@maxcode AS INT)), 0) + 1
	SET @result = RIGHT('000'+CAST(@tmp AS VARCHAR),4)
  END
  return @result;
END
```



#### GenCode_All 生成编号

> 目前就只针对三个表进行了生成
>
> 后续需要哪个表时  新增一下表名那里的处理就好

```sql
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


ALTER FUNCTION [dbo].[GenCode_All] 
(
	@TableName NVARCHAR(20),
    @VCHRTYPE CHAR(4), --单别
	@SDATE DATETIME =NULL  --日期
)
RETURNS VARCHAR(11)
AS
BEGIN
	IF @SDATE IS NULL
		SET @SDATE = GETDATE()

  DECLARE @y VARCHAR(4) SET @y = CAST(YEAR(@SDATE) AS VARCHAR)  
  DECLARE @m VARCHAR(2) SET @m = RIGHT('0'+LTRIM(MONTH(@SDATE)),2) 
  DECLARE @d VARCHAR(2) SET @d = RIGHT('0'+LTRIM(DAY(@SDATE)),2) 
  DECLARE @FType CHAR(1) --单据编码方式 1.日编、2.月编、3.流水号、4.手动编号
  DECLARE @SerialLen INT --流水号长度
  DECLARE @YearLen INT --年长度
  DECLARE @result VARCHAR(11) 
  SELECT @FType = MQ004,@YearLen=MQ005,@SerialLen=MQ006 FROM CMSMQ WHERE MQ001=@VCHRTYPE;   --单规则
  SET @y=RIGHT(@y,@YearLen);   
  IF @FType = '1'
  BEGIN
    --日编 年+月+日+流水号
    SET @result = @y + @m + @d;
  END
  ELSE IF @FType = '2'
  BEGIN
    --月编 年+月+流水号
    SET @result = @y + @m ;
  END  
  ELSE IF @FType = '3'
  BEGIN
    --流水号 流水号
    SET @result = '';
  END  
  ELSE 
    RETURN '';
  DECLARE @maxcode VARCHAR(11), @maxserial VARCHAR(11)   
  DECLARE @likestr VARCHAR(11)  SET @likestr= @result + '%';
  
  SET @maxcode = CASE
					WHEN @TableName = 'BOMCA' THEN (SELECT MAX(CA002) FROM BOMCA WHERE CA001=@VCHRTYPE AND CA002 LIKE @likestr)	-- 入库
					WHEN @TableName = 'PURCC' THEN (SELECT MAX(CC002) FROM PURCC WHERE CC001=@VCHRTYPE AND CC002 LIKE @likestr)	-- 到货单
					WHEN @TableName = 'PURTG' THEN (SELECT MAX(TG002) FROM PURTG WHERE TG001=@VCHRTYPE AND TG002 LIKE @likestr)	-- 到货单
					ELSE NULL
				 END
  IF @maxcode IS NULL
    SET @maxserial = RIGHT('0000000001', @SerialLen)
  ELSE
    SET @maxserial = RIGHT('0000000000' + CAST(CAST(RIGHT(LTRIM(@maxcode),@SerialLen) AS INT) + 1 AS VARCHAR),@SerialLen); 
  SET @result = @result + @maxserial;  
  RETURN @result;
END

```







| 订单号   | 品号  | 数量 | 发货日     | 发货数量 |
| -------- | ----- | ---- | ---------- | -------- |
| order001 | CP001 | 20   | 2024-04-01 | 20       |
| order001 | CP001 | 50   | 2024-04-03 | 50       |
| order001 | CP001 | 70   | 2024-04-05 | 70       |





| 订单号   | 品号  | 数量 | 第一次发货日 | 第一次发货数量 | 第二次发货日 | 第二次发货数量 | 第三次发货日 | 第三次发货数量 |
| -------- | ----- | ---- | ------------ | -------------- | ------------ | -------------- | ------------ | -------------- |
| order001 | CP001 | 20   | 2024-04-01   | 20             | 2024-04-03   | 50             | 2024-04-05   | 70             |

